if [[ "$TERM_PROGRAM" == "vscode" && ! -t 1 ]]; then
  return
fi

printf "\e[?1042l"

export PATH="$PATH:$HOME/.local/scripts"

ZSH_THEME='gruvbox'
# SOLARIZED_THEME="dark"
# source "$HOME/.oh-my-zsh/oh-my-zsh.sh"

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

plugins=(git zsh-syntax-highlighting zsh-autosuggestions powerlevel10k tmux)

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion


export GPG_TTY=$(tty)


alias v="nvim"

# Set up fzf key bindings and fuzzy completion
source <(fzf --zsh)
bindkey -s '^F' "tmux-git-sessionizer\n"

#determines search program for fzf
if type ag &> /dev/null; then
    export FZF_DEFAULT_COMMAND='ag -p ~/.gitignore -g ""'
fi
#refer rg over ag
if type rg &> /dev/null; then
    export FZF_DEFAULT_COMMAND='rg --files --hidden'
fi

# cd /Users/ryenbeatty/Code/
alias reload!='source ~/.zshrc'

# Auto-start tmux session when entering project directories
cd() {
    builtin cd "$@" || return
    
    # Only activate for git repositories or directories with specific files
    if [[ -d .git ]] || [[ -f package.json ]] || [[ -f Cargo.toml ]]; then
        # Only run if we're not already in tmux
        if [[ -z "$TMUX" ]]; then
            session_name=$(basename "$PWD" | tr . _)
            
            # Check if session exists
            if ! tmux has-session -t="$session_name" 2> /dev/null; then
                # Create new session in background
                tmux new-session -d -s "$session_name"
            fi
            
            # Attach to the session
            tmux attach-session -t "$session_name"
        fi
    fi
}

alias gittail='watch -n 1 -c "git log --graph --pretty=format:'"'"'%Cred%h%Creset -%C(yellow)%d%Creset %s %Cgreen(%cr) %C(bold blue)<%an>%Creset'"'"' --abbrev-commit -15"'

alias notify='afplay /System/Library/Sounds/Funk.aiff'

